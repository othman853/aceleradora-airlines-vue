buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    compile "com.h2database:h2"
    compile "org.postgresql:postgresql:42.2.5"
    compile "org.flywaydb:flyway-core"
    compile "org.projectlombok:lombok:1.16.18"

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.21.0"
}

repositories {
    mavenCentral()
}

task startFromJar(type: Exec, dependsOn: assemble, group: 'Local Development') {
    description = "Assembles app in a jar and runs it on the background"

    commandLine "${rootDir}/scripts/local-test-server", "start", "$buildDir/libs/app.jar", "$buildDir"
}

task stopRunningJar(type: Exec, group: 'Local Development') {
    description = "Stops a running app previously started by :app:startFromJar task"

    commandLine "${rootDir}/scripts/local-test-server", "stop", "$buildDir/libs/app.jar", "$buildDir"
}

task dbSetup(type: Exec, group: 'Local Development') {
    description = "Spins up a new Postgres docker container for local development"

    commandLine "${rootDir}/scripts/local-database", "setup"
}

task dbStart(type: Exec, group: 'Local Development') {
    description = "Starts a previously created Postgres docker container"

    commandLine "${rootDir}/scripts/local-database", "start"
}

task dbContainerStop(type: Exec, group: 'Local Development') {
    description = "Stops a running Postgres docker container"

    commandLine "${rootDir}/scripts/local-database", "stop"
}

task dbContainerRemove(type: Exec, group: 'Local Development') {
    description = "Removes the Postgres docker container created for this application"

    commandLine "${rootDir}/scripts/local-database", "remove"
}

bootRun {
    sourceResources sourceSets.main
    jvmArgs = [
            "-Dspring.profiles.active=dev",
            "-Dspring.resources.chain.cache=false"
    ]
}

bootRun.dependsOn(dbStart)

build.mustRunAfter('clean')
